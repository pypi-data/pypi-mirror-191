使下 游戏
使下 系统
使下 随机数

讲嘢 -> {
    |大小| 系 畀个tuple(640, 480)
    |吃咗| 系 1
    |初始方向| 系 "右"
    |方向| 系 |初始方向|
    |食物总数| 系 1
}

|校色| 下 -> |畀个tuple(255, 0, 0)| @ |红色|
|校色| 下 -> |畀个tuple(0, 0, 0)| @ |黑色|
|校色| 下 -> |畀个tuple(255, 255, 255)| @ |白色|

|屏幕老作| 下 -> |大小, "贪吃蛇"| @ |屏幕|

|100, 100| 拍住上 -> |初始位置|
|[100, 100], [80, 100], [60, 100], [40, 100], [20, 100]| 拍住上 -> |蛇身|
|300, 300| 拍住上 -> |目标位置|
|300, 300| 拍住上 -> |食物位置|

$揿掣 |e| 要做咩
    Share下 初始位置
    Share下 方向
    Share下 初始方向

    如果 |摞掣(e) 系 100| 嘅话 -> {
        讲嘢: |方向| 系 "右"
    }

    如果 |摞掣(e) 系 97| 嘅话 -> {
        讲嘢: |方向| 系 "左"
    }

    如果 |摞掣(e) 系 119| 嘅话 -> {
        讲嘢: |方向| 系 "上"
    }

    如果 |摞掣(e) 系 115| 嘅话 -> {
        讲嘢: |方向| 系 "下"
    }

搞掂

$游戏结束 要做咩
    收工
搞掂

有条仆街叫 |事件|
顶你 -> |事件| : |['KEYDOWN', 揿掣]|

落操场玩跑步
    讲嘢: |时间| 系 |计时器()|
    |上画| 下 -> |屏幕 = 屏幕, event = 事件|

     /*
     *  判断是否输入了反方向运动
    */

    如果 |方向 系 "右" 同埋 初始方向 != "左"| 嘅话 -> {
        讲嘢: |初始方向| 系 |方向|
    }

    如果 |方向 系 "左" 同埋 初始方向 != "右"| 嘅话 -> {
        讲嘢: |初始方向| 系 |方向|
    }

    如果 |方向 系 "上" 同埋 初始方向 != "下"| 嘅话 -> {
        讲嘢: |初始方向| 系 |方向|
    }

    如果 |方向 系 "下" 同埋 初始方向 != "上"| 嘅话 -> {
        讲嘢: |初始方向| 系 |方向|
    }

    如果 |初始方向 系 "右"| 嘅话 -> {
        讲嘢: |初始位置[0]| 系 |初始位置[0] + 20|
    }

    如果 |初始方向 系 "左"| 嘅话 -> {
        讲嘢: |初始位置[0]| 系 |初始位置[0] - 20|
    }

    如果 |初始方向 系 "上"| 嘅话 -> {
        讲嘢: |初始位置[1]| 系 |初始位置[1] - 20|
    }

    如果 |初始方向 系 "下"| 嘅话 -> {
        讲嘢: |初始位置[1]| 系 |初始位置[1] + 20|
    }

    /* 增加蛇的长度 */
    用下 |挜位(蛇身, 0, list(初始位置))|

    如果 |初始位置[0] 系 食物位置[0] 同埋 初始位置[1] 系 食物位置[1]| 嘅话 -> {
        畀我睇下 "好好味" 点样先?
        讲嘢: |食物总数| 系 0
    }

    唔系 嘅话 -> {
        用下 |蛇身.pop()|
    }

    如果 |食物总数 系 0| 嘅话 -> {
        讲嘢 -> {
            x 係 |求其嚟个(1, 32)|
            y 係 |求其嚟个(1, 24)|
            食物总数 係 1
        }
        |int(x * 20), int(y * 20)| 拍住上 -> |食物位置|
    }

    |屏幕校色| 下 -> |屏幕, 黑色|

    |位置| 从 0 行到 |蛇身 嘅长度|
        矩形老作 下 -> |屏幕, 白色, 蛇身[位置][0], 蛇身[位置][1]|
        矩形老作 下 -> |屏幕, 红色, 食物位置[0], 食物位置[1]|
    行晒

    |刷新| 下 -> ||

    /*
     * 判断游戏是否结束
     */
    如果 |初始位置[0] > 620 或者 初始位置[0] < 0| 嘅话 -> {
        畀我睇下 "撞墙啦!" 点样先?
        用下 |游戏结束()|
    }

    定系 |初始位置[1] > 420 或者 初始位置[1] < 0| 嘅话 -> {
        畀我睇下 "撞墙啦!" 点样先?
        用下 |游戏结束()|
    }
    
    用下 |睇表(时间, 5)|

玩到 |唔啱| 为止